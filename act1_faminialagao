#include <WiFi.h>
#include <WebServer.h>

const char* ssid = "MayAnn";
const char* password = "12345678";

WebServer server(80);

const int ledPins[] = {19, 5, 16, 0, 15};
bool allOn = false;

void setup() {
  Serial.begin(115200);
  connectWiFi();
  initializeLEDs();
  setupServerRoutes();
  server.begin();
}

void loop() {
  server.handleClient();
}

void connectWiFi() {
  WiFi.begin(ssid, password);
  Serial.print("Connecting to WiFi...");
  while (WiFi.status() != WL_CONNECTED) {
    delay(1000);
    Serial.print(".");
  }
  Serial.println("\nConnected to WiFi");
  Serial.print("IP Address: ");
  Serial.println(WiFi.localIP());
}

void initializeLEDs() {
  for (int pin : ledPins) {
    pinMode(pin, OUTPUT);
    digitalWrite(pin, LOW);
  }
}

void setupServerRoutes() {
  server.on("/", serveWebPage);
  server.on("/LED=ALL", handleLEDControl);
  server.on("/LED=SEQ1", handleLEDControl);
  server.on("/LED=SEQ2", handleLEDControl);
}

void serveWebPage() {
  String html = "<!DOCTYPE html><html><head><title>ESP32 LED Control</title>";
  html += "<meta name=\"viewport\" content=\"width=device-width, initial-scale=1.0\">";
  html += "<h1>ESP32 LED Control</h1>";
  html += "<button onclick=\"location.href='/LED=ALL'\">All LEDs</button>";
  html += "<button onclick=\"location.href='/LED=SEQ1'\">Sequence A</button>";
  html += "<button onclick=\"location.href='/LED=SEQ2'\">Sequence B</button>";
  html += "</body></html>";

  server.send(200, "text/html", html);
}

void handleLEDControl() {
  String request = server.uri();
  if (request == "/LED=ALL") {
    toggleAllLEDs();
  } else if (request == "/LED=SEQ1") {
    playSequence1();
  } else if (request == "/LED=SEQ2") {
    playSequence2();
  }
  server.sendHeader("Location", "/");
  server.send(303);
}

void toggleAllLEDs() {
  allOn = !allOn;
  for (int pin : ledPins) {
    digitalWrite(pin, allOn ? HIGH : LOW);
  }
}

void playSequence1() {
  for (int pin : ledPins) {
    digitalWrite(pin, HIGH);
    delay(250);
    digitalWrite(pin, LOW);
  }
}

void playSequence2() {
  runPattern(sequenceOne);
  runPattern(sequenceTwo);
  runPattern(sequenceThree);
  runPattern(sequenceTwo);
  runPattern(sequenceOne);
  clearLEDs();
}

void runPattern(void (*pattern)()) {
  pattern();
  delay(250);
}

void clearLEDs() {
  for (int pin : ledPins) {
    digitalWrite(pin, LOW);
  }
}

void sequenceOne() { clearLEDs(); digitalWrite(16, HIGH); }
void sequenceTwo() { clearLEDs(); digitalWrite(5, HIGH); digitalWrite(0, HIGH); }
void sequenceThree() { clearLEDs(); digitalWrite(19, HIGH); digitalWrite(15, HIGH); }
